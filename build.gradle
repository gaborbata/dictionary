apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'dk.brics', name: 'automaton', version: '1.12-1'
    implementation group: 'com.formdev', name: 'flatlaf', version: '3.2.5'
    implementation group: 'com.formdev', name: 'svgSalamander', version: '1.1.4'
}


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = 1.8
}

jar {
    archiveBaseName = 'dictionary'
    archiveVersion = '0.4.0'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': 'metis.dictionary.Dictionary')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude '**/*.aut'
}

task checkDataFileFormat {
    doLast {
        File file = new File("${projectDir}/src/main/resources/resources/dictionary.data")
        file.eachLine { line, count ->
            if (!(line ==~ / .+ :: .+ /)) {
                throw new GradleException("Incorrect data file format, line: ${count}, content:'${line}'")
            }
        }
    }
}

check.dependsOn checkDataFileFormat
